apply plugin: 'com.android.library'

def safeExtGet(prop, key, fallback) {
    if (rootProject.ext.has(prop)) {
        def map = rootProject.ext.get(prop)
        if (map.containsKey(key)) {
            return map.get(key)
        }
    }
    return fallback
}

// add the maven publish script if existed
def maven_gradle_path = "${rootDir.absolutePath}/maven.gradle"
if (new File(maven_gradle_path).exists()) {
    apply from: maven_gradle_path
}

// special the version using when publishing to maven
// the group name is defined in setting.gradle
project.group="io.github.agoraio-usecase.meeting"
project.version="${safeExtGet('meeting', 'versionName', '1.0.0')}"
project.description = "AgoraMeetingScreenSharing"
project.ext.url = "https://bitbucket.agoralab.co/projects/ADUC/repos/cloudmeeting-android/browse/AgoraMeeting_Android/screensharing?at=refs%2Fheads%2Fdev%2Fandroid_210114"

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode safeExtGet('meeting', 'versionCode', 1)
        versionName safeExtGet('meeting', 'versionName', '1.0.0')

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:${safeExtGet('dependencies', 'androidx.appcompat:appcompat', '1.1.0')}"
    implementation "io.agora.rtc:full-sdk:${safeExtGet('dependencies', 'io.agora.rtc:full-sdk', '3.2.1')}"
    implementation "io.agora.rtm:rtm-sdk:${safeExtGet('dependencies', 'io.agora.rtm:rtm-sdk', '1.2.2')}"
    implementation "com.github.AgoraIO-Usecase:Tools:${safeExtGet('dependencies', 'com.github.AgoraIO-Usecase:Tools', 'log_java_1.0.4')}"
}
